plugins {
    id 'idea'
    id 'java'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '6.0.24'
}

version = mod_version
group = mod_group_id

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
    withSourcesJar()
}

base {
    archivesName = mod_id
}

minecraft {
    mappings channel: 'official', version: minecraft_version

    copyIdeResources = true

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--username', 'Player'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--nogui'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenLocal()

    maven { url = "https://maven.createmod.net" }
    maven { url = "https://mvn.devos.one/snapshots" }
    maven { url = "https://maven.blamejared.com" }
    maven { url = "https://www.cursemaven.com" }
    maven { url = "https://api.modrinth.com/maven" }
    maven { url = "https://modmaven.dev" }
    maven { url = "https://maven.architectury.dev" }
    maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven" }
    maven { url = "https://maven.ftb.dev/releases" }
    maven { url = "https://dl.zznty.ru/maven" }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    implementation fg.deobf("com.simibubi.create:create-${minecraft_version}:${create_version}:slim")
    implementation fg.deobf("net.createmod.ponder:Ponder-Forge-${minecraft_version}:${ponder_version}")
    compileOnly fg.deobf("dev.engine-room.flywheel:flywheel-forge-api-${flywheel_minecraft_version}:${flywheel_version}")
    runtimeOnly fg.deobf("dev.engine-room.flywheel:flywheel-forge-${flywheel_minecraft_version}:${flywheel_version}")
    implementation fg.deobf("com.tterrag.registrate:Registrate:${registrate_version}")
    implementation fg.deobf("ru.zznty:create_factory_abstractions-${minecraft_version}:${create_factory_abstractions_version}")
    implementation fg.deobf("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")

    compileOnly fg.deobf("curse.maven:create-more-pipe-bombs-in-packages-1304635:6755828")
    compileOnly fg.deobf("curse.maven:create-factory-logistics-1218807:6697752")

    runtimeOnly fg.deobf("curse.maven:create-more-linked-remote-1277244:6638807")
    runtimeOnly fg.deobf("curse.maven:create-factory-logistics-1218807:6697752")
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            forge_version          : forge_version,
            forge_version_range    : forge_version_range,
            loader_version_range   : loader_version_range,
            create_version         : create_version,
            create_version_range   : create_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}

sourceSets.main.resources.srcDir generateModMetadata

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
